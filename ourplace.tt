#class {ourplace} open
/*
The ourplace class is the entry point for ourplace MUD.  It is best to 
have tintin load this class at startup.  To do that, invoke tt++ with this
file as an argument 'tt++ <tintin-scripts-dir>/ourplace.tt'.  As part of that 
script, you will also need to TINTIN_ROOT to the directory above where you 
installed tintin-ourplace-mud.
*/

/*
These are global configurations for ourplace.
*/

#script {root} {echo $TINTIN_ROOT};
#var tmp {/tmp};
#var dir {$root[1]/tintin-ourplace-mud};
#var char {};
#var mud {ourplace.fractured.org};
#var port {6543};
#var logdir {logs};

/*
Contains variable values that pertain only to the local system, such as passwords.
*/
#read $dir/local.tt

/*
load-char is the alias to enable the various character support options.
Loading the character loads various class-related skill aliases and actions.
It also turns on logging to your logs directory.
*/
#alias {load-char %1} {
	#unalias {.$char};
	#class $char kill;
	#var char %1;
	#class $char read $dir/$char.tt;
	#if {&{password-$char}} {#var pw ${password-$char}} {#var pw $password};
	#alias {.$char} 
	{
		#ses .$char $mud $port; $char; $pw; who;
		#send {prompt <%h/%Hhp (%P) %m/%Mm %v/%Vmv, opp: %O, leader: %L>};
		#prompt {<%%%1hp (%%%2) %%%3m %%%4mv, opp: %%%5, leader: %%%6>} {<199><AAE><ffa>H: %%%1 (%%%2) M: %%%3 MV: %%%4 OPP: %%%5 LDR: %%%6<099>} {2};
		#split 0 2;
	};
	#echo {$char loaded.};
};

#event {SESSION CONNECTED}
{
	#config log html;
	#format {logfile} {$logdir/$mud-$char-%t.html} {%Y-%m-%d};
	#log append $logfile;
	#echo {logging to $logfile};
};

/*
	Some terminals are capable of displaying colors, but have no colors initialized, or 
	have the colors initialized to rather odd values. The following script will set them
	to default values. This might not work for every terminal.
*/
#alias {colorinit}
{
     #showme \e]P0000000 <008>black;
     #showme \e]P1AA0000 <018>red;
     #showme \e]P200AA00 <028>green;
     #showme \e]P3AAAA00 <038>yellow;
     #showme \e]P40000AA <048>blue;
     #showme \e]P5AA00AA <058>magenta;
     #showme \e]P600AAAA <068>cyan;
     #showme \e]P7AAAAAA <078>white;
     #showme \e]P8555555 <108>light black;
     #showme \e]P9FF5555 <118>light red;
     #showme \e]PA55FF5A <128>light green;
     #showme \e]PBFFFF55 <138>light yellow;
     #showme \e]PC5555FF <148>light blue;
     #showme \e]PDFF55FF <158>light magenta;
     #showme \e]PE55FFFF <168>light cyan;
     #showme \e]PFFFFFFF <178>light white;
};

#alias {colorreset}
{
     #showme \e]R Colors reset to default.
};

/*
	Terminals capable of xterm 256 colors will display a color pallet from which you
	can copy the accompanying color codes.
*/
#alias {colorpalette}
{
	#foreach {a;b;c;d;e;f} {cnt1}
	{
		#foreach {a;b;c;d;e;f} {cnt2}
		{
			#var result <000>;
			#foreach {a;b;c;d;e;f} {cnt3}
			{
				#var result $result  <$cnt1$cnt2$cnt3> <<888>$cnt1$cnt2$cnt3><088>
			};
			#showme $result
		};
	};
};

#class {ourplace} close
